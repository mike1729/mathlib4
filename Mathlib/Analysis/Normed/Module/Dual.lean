/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
module

public import Mathlib.Analysis.LocallyConvex.Polar
public import Mathlib.Analysis.Normed.Module.RCLike.Basic
public import Mathlib.Data.Set.Finite.Lemmas
public import Mathlib.Analysis.LocallyConvex.AbsConvex
public import Mathlib.Analysis.Normed.Module.Convex
public import Mathlib.Analysis.RCLike.Lemmas

/-!
# Polar sets in the strong dual of a normed space

In this file we study polar sets in the strong dual `StrongDual` of a normed space.

## Main definitions

* `polar ğ•œ s` is the subset of `StrongDual ğ•œ E` consisting of those functionals `x'` for which
  `â€–x' zâ€– â‰¤ 1` for every `z âˆˆ s`.

## References

* [Conway, John B., A course in functional analysis][conway1990]

## Tags

strong dual, polar
-/

@[expose] public section

noncomputable section

open Topology Bornology

namespace NormedSpace

section PolarSets

open Metric Set StrongDual

variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]

theorem isClosed_polar (s : Set E) : IsClosed (StrongDual.polar ğ•œ s) := by
  dsimp only [StrongDual.polar]
  simp only [LinearMap.polar_eq_iInter, LinearMap.flip_apply]
  refine isClosed_biInter fun z _ => ?_
  exact isClosed_Iic.preimage (ContinuousLinearMap.apply ğ•œ ğ•œ z).continuous.norm

@[simp]
theorem polar_closure (s : Set E) : StrongDual.polar ğ•œ (closure s) = StrongDual.polar ğ•œ s :=
  ((topDualPairing ğ•œ E).flip.polar_antitone subset_closure).antisymm <|
    (topDualPairing ğ•œ E).flip.polar_gc.l_le <|
      closure_minimal ((topDualPairing ğ•œ E).flip.polar_gc.le_u_l s) <| by
        simpa [LinearMap.flip_flip] using
          (isClosed_polar _ _).preimage (ContinuousLinearMap.apply ğ•œ ğ•œ (E := E)).continuous

variable {ğ•œ}

/-- If `x'` is a `StrongDual ğ•œ E` element such that the norms `â€–x' zâ€–` are bounded for `z âˆˆ s`, then
a small scalar multiple of `x'` is in `polar ğ•œ s`. -/
theorem smul_mem_polar {s : Set E} {x' : StrongDual ğ•œ E} {c : ğ•œ} (hc : âˆ€ z, z âˆˆ s â†’ â€–x' zâ€– â‰¤ â€–câ€–) :
    câ»Â¹ â€¢ x' âˆˆ StrongDual.polar ğ•œ s := by
  by_cases c_zero : c = 0
  Â· simp only [c_zero, inv_zero, zero_smul]
    exact (topDualPairing ğ•œ E).flip.zero_mem_polar _
  have eq : âˆ€ z, â€–câ»Â¹ â€¢ x' zâ€– = â€–câ»Â¹â€– * â€–x' zâ€– := fun z => norm_smul câ»Â¹ _
  have le : âˆ€ z, z âˆˆ s â†’ â€–câ»Â¹ â€¢ x' zâ€– â‰¤ â€–câ»Â¹â€– * â€–câ€– := by
    intro z hzs
    rw [eq z]
    apply mul_le_mul (le_of_eq rfl) (hc z hzs) (norm_nonneg _) (norm_nonneg _)
  have cancel : â€–câ»Â¹â€– * â€–câ€– = 1 := by
    simp only [c_zero, norm_eq_zero, Ne, not_false_iff, inv_mul_cancelâ‚€, norm_inv]
  rwa [cancel] at le

set_option backward.isDefEq.respectTransparency false in
theorem polar_ball_subset_closedBall_div {c : ğ•œ} (hc : 1 < â€–câ€–) {r : â„} (hr : 0 < r) :
    StrongDual.polar ğ•œ (ball (0 : E) r) âŠ† closedBall (0 : StrongDual ğ•œ E) (â€–câ€– / r) := by
  intro x' hx'
  rw [StrongDual.mem_polar_iff] at hx'
  simp only [mem_closedBall_zero_iff, mem_ball_zero_iff] at *
  have hcr : 0 < â€–câ€– / r := div_pos (zero_lt_one.trans hc) hr
  refine ContinuousLinearMap.opNorm_le_of_shell hr hcr.le hc fun x hâ‚ hâ‚‚ => ?_
  calc
    â€–x' xâ€– â‰¤ 1 := hx' _ hâ‚‚
    _ â‰¤ â€–câ€– / r * â€–xâ€– := (inv_le_iff_one_le_mulâ‚€' hcr).1 (by rwa [inv_div])

variable (ğ•œ)

theorem closedBall_inv_subset_polar_closedBall {r : â„} :
    closedBall (0 : StrongDual ğ•œ E) râ»Â¹ âŠ† StrongDual.polar ğ•œ (closedBall (0 : E) r) :=
  fun x' hx' x hx =>
  calc
    â€–x' xâ€– â‰¤ â€–x'â€– * â€–xâ€– := x'.le_opNorm x
    _ â‰¤ râ»Â¹ * r :=
      (mul_le_mul (mem_closedBall_zero_iff.1 hx') (mem_closedBall_zero_iff.1 hx) (norm_nonneg _)
        (dist_nonneg.trans hx'))
    _ = r / r := inv_mul_eq_div _ _
    _ â‰¤ 1 := div_self_le_one r

set_option backward.isDefEq.respectTransparency false in
/-- The `polar` of closed ball in a normed space `E` is the closed ball of the dual with inverse
radius. -/
theorem polar_closedBall {ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] {r : â„}
    (hr : 0 < r) :
    StrongDual.polar ğ•œ (closedBall (0 : E) r) = closedBall (0 : StrongDual ğ•œ E) râ»Â¹ := by
  refine Subset.antisymm ?_ (closedBall_inv_subset_polar_closedBall ğ•œ)
  intro x' h
  simp only [mem_closedBall_zero_iff]
  refine ContinuousLinearMap.opNorm_le_of_ball hr (inv_nonneg.mpr hr.le) fun z _ => ?_
  simpa only [one_div] using LinearMap.bound_of_ball_bound' hr 1 x'.toLinearMap h z

set_option backward.isDefEq.respectTransparency false in
theorem polar_ball {ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] {r : â„}
    (hr : 0 < r) : StrongDual.polar ğ•œ (ball (0 : E) r) = closedBall (0 : StrongDual ğ•œ E) râ»Â¹ := by
  apply le_antisymm
  Â· intro x hx
    rw [mem_closedBall_zero_iff]
    apply le_of_forall_gt_imp_ge_of_dense
    intro a ha
    rw [â† mem_closedBall_zero_iff, â† (mul_div_cancel_leftâ‚€ a (Ne.symm (ne_of_lt hr)))]
    rw [â† RCLike.norm_of_nonneg (K := ğ•œ) (le_trans zero_le_one
      (le_of_lt ((inv_lt_iff_one_lt_mulâ‚€' hr).mp ha)))]
    apply polar_ball_subset_closedBall_div _ hr hx
    rw [RCLike.norm_of_nonneg (K := ğ•œ) (le_trans zero_le_one
      (le_of_lt ((inv_lt_iff_one_lt_mulâ‚€' hr).mp ha)))]
    exact (inv_lt_iff_one_lt_mulâ‚€' hr).mp ha
  Â· rw [â† polar_closedBall hr]
    exact LinearMap.polar_antitone _ ball_subset_closedBall

/-- Given a neighborhood `s` of the origin in a normed space `E`, the dual norms of all elements of
the polar `polar ğ•œ s` are bounded by a constant. -/
theorem isBounded_polar_of_mem_nhds_zero {s : Set E} (s_nhds : s âˆˆ ğ“ (0 : E)) :
    IsBounded (StrongDual.polar ğ•œ s) := by
  obtain âŸ¨a, haâŸ© : âˆƒ a : ğ•œ, 1 < â€–aâ€– := NormedField.exists_one_lt_norm ğ•œ
  obtain âŸ¨r, r_pos, r_ballâŸ© : âˆƒ r : â„, 0 < r âˆ§ ball 0 r âŠ† s := Metric.mem_nhds_iff.1 s_nhds
  exact isBounded_closedBall.subset
    (((topDualPairing ğ•œ E).flip.polar_antitone r_ball).trans <|
      polar_ball_subset_closedBall_div ha r_pos)

theorem sInter_polar_eq_closedBall {ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {r : â„} (hr : 0 < r) :
    â‹‚â‚€ (StrongDual.polar ğ•œ '' { F | F.Finite âˆ§ F âŠ† closedBall (0 : E) râ»Â¹ }) = closedBall 0 r := by
  conv_rhs => rw [â† inv_inv r]
  rw [â† polar_closedBall (inv_pos_of_pos hr), StrongDual.polar,
    (topDualPairing ğ•œ E).flip.sInter_polar_finite_subset_eq_polar (closedBall (0 : E) râ»Â¹)]

end PolarSets

end NormedSpace

namespace LinearMap

section NormedField

variable {ğ•œ E F : Type*}
variable [RCLike ğ•œ] [AddCommMonoid E] [AddCommMonoid F]
variable [Module ğ•œ E] [Module ğ•œ F]

variable {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} (s : Set E)

open ComplexOrder in
theorem polar_AbsConvex : AbsConvex ğ•œ (B.polar s) := by
  rw [polar_eq_biInter_preimage]
  exact AbsConvex.iInterâ‚‚ fun i hi =>
    âŸ¨balanced_closedBall_zero.mulActionHom_preimage (f := (B i : (F â†’â‚‘[(RingHom.id ğ•œ)] ğ•œ))),
      (convex_RCLike_iff_convex_real.mpr (convex_closedBall 0 1)).linear_preimage _âŸ©

end NormedField

end LinearMap
