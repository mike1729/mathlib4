/-
Copyright (c) 2026 MichaÅ‚ ÅšwiÄ™tek. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: MichaÅ‚ ÅšwiÄ™tek
-/
module

public import Mathlib.Analysis.Normed.Module.SchauderBasis.BasicSequence
public import Mathlib.Topology.MetricSpace.HausdorffDistance
public import Mathlib.Topology.MetricSpace.ProperSpace
public import Mathlib.Topology.Neighborhoods

/-!
# Selection Principle for Basic Sequences

The **selection principle** extracts basic sequences from sets that are weak*-dense near the
origin but norm-separated from it. This gap between the weak and norm topologies is the
mechanism behind many structural results in Banach space theory.

The key technical ingredient is a perturbation lemma: given a finite-dimensional subspace and a
weak*-dense set, one can find an element of the set that is almost orthogonal to the subspace.
Iterating this produces a sequence satisfying the GrÃ¼nblum condition, hence a basic sequence.

As a corollary, every infinite-dimensional Banach space contains a basic sequence with basis
constant arbitrarily close to 1 (the Bessagaâ€“PeÅ‚czyÅ„ski theorem).

## Main Results

* `perturbation_finite_dimensional`: Given a weak*-dense set and a finite-dimensional subspace,
  there exists a perturbation element almost orthogonal to the subspace.
* `basic_sequence_selection_dual`: The dual selection principle â€” extracts a basic sequence
  from a set that is weak*-dense near 0 but norm-separated from 0.
* `weak_closure_sphere_contains_zero`: In an infinite-dimensional space, 0 is in the weak*
  closure of the unit sphere's image in the bidual.
* `exists_basicSequence`: Every infinite-dimensional Banach space contains a basic sequence
  with basis constant arbitrarily close to 1.

## References

* [F. Albiac, N.J. Kalton, *Topics in Banach Space Theory*][albiac2016]
-/

@[expose] public section

noncomputable section

open Submodule Set WeakDual Metric Filter Topology

variable {ğ•œ : Type*} [RCLike ğ•œ]
variable {X : Type*} [NormedAddCommGroup X] [NormedSpace ğ•œ X]

namespace BasicSequence

/-- Given a weak*-dense set `S` norm-separated from 0 and a finite-dimensional subspace `E`,
    there exists `x âˆˆ S` that is almost orthogonal to `E`: for all `e âˆˆ E` and scalars `c`,
    `â€–e + c â€¢ xâ€– â‰¥ (1 - Îµ) * â€–eâ€–`. -/
lemma perturbation_finite_dimensional {S : Set (StrongDual ğ•œ X)}
    (h_weak_star : (0 : WeakDual ğ•œ X) âˆˆ closure (StrongDual.toWeakDual '' S))
    (h_norm : (0 : StrongDual ğ•œ X) âˆ‰ closure S)
    (E : Subspace ğ•œ (StrongDual ğ•œ X))
    (hefind : FiniteDimensional ğ•œ E)
    {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ x âˆˆ S, âˆ€ (e : E) (c : ğ•œ), â€–(e : StrongDual ğ•œ X) + c â€¢ xâ€– â‰¥ (1 - Îµ) * â€–eâ€– := by
  have hS_nonempty : S.Nonempty := by
    by_contra h; rw [Set.not_nonempty_iff_eq_empty] at h; simp [h] at h_weak_star
  rcases le_or_gt 1 Îµ with hÎµ1 | hÎµ1
  Â· obtain âŸ¨x, hxSâŸ© := hS_nonempty
    exact âŸ¨x, hxS, fun e c => le_trans
      (mul_nonpos_of_nonpos_of_nonneg (by linarith) (norm_nonneg _)) (norm_nonneg _)âŸ©
  let Î´ := Metric.infDist (0 : StrongDual ğ•œ X) S
  have hÎ´ : 0 < Î´ := (Metric.infDist_pos_iff_notMem_closure hS_nonempty).mp h_norm
  let M := 2 / Î´
  let Î³ := Îµ * Î´ / 4
  have h_norm_S : âˆ€ x âˆˆ S, Î´ â‰¤ â€–xâ€– := fun x hx =>
    (Metric.infDist_le_dist_of_mem hx).trans_eq (dist_zero_left x)
  let sphere := Metric.sphere (0 : E) 1
  let U (v : {v : X // â€–vâ€– â‰¤ 1}) : Set E :=
    {e | 1 - Îµ / 2 < â€–(e : StrongDual ğ•œ X) vâ€–}
  have h_cover : sphere âŠ† â‹ƒ v, U v := by
    intro e he
    rw [mem_sphere_zero_iff_norm] at he
    have h_lt : 1 - Îµ / 2 < â€–(e : StrongDual ğ•œ X)â€– := by
      rw [norm_coe, he]
      linarith
    obtain âŸ¨v, hv, hv_valâŸ© :=
      ContinuousLinearMap.exists_lt_apply_of_lt_opNorm (e : StrongDual ğ•œ X) h_lt
    exact Set.mem_iUnion.mpr âŸ¨âŸ¨v, hv.leâŸ©, hv_valâŸ©
  have h_open (v : {v : X // â€–vâ€– â‰¤ 1}) : IsOpen (U v) := by
    have : Continuous fun (e : E) => (e : StrongDual ğ•œ X) v.val :=
      (ContinuousLinearMap.apply ğ•œ ğ•œ v.val).continuous.comp continuous_subtype_val
    exact isOpen_Ioi.preimage (Continuous.norm this)
  obtain âŸ¨F, hF_coverâŸ© := (isCompact_sphere (0 : E) 1).elim_finite_subcover U h_open h_cover
  let W := {w : WeakDual ğ•œ X | âˆ€ v âˆˆ F, â€–w vâ€– < Î³}
  have hW_open : IsOpen W := by
    rw [show W = â‹‚ v âˆˆ F, {w | â€–w vâ€– < Î³} by ext; simp [W]]
    apply isOpen_biInter_finset
    intro v _
    refine isOpen_lt (continuous_norm.comp (WeakDual.eval_continuous (v : X))) continuous_const
  have hÎ³ : 0 < Î³ := by
    dsimp [Î³]
    nlinarith [hÎµ, hÎ´]
  have hW0 : (0 : WeakDual ğ•œ X) âˆˆ W := fun _ _ => by
    rw [ContinuousLinearMap.zero_apply, norm_zero]; exact hÎ³
  obtain âŸ¨_, hwW, âŸ¨x, hxS, rflâŸ©âŸ© : âˆƒ w âˆˆ W, âˆƒ x âˆˆ S, StrongDual.toWeakDual x = w :=
      (_root_.mem_closure_iff).mp h_weak_star W hW_open hW0
  refine âŸ¨x, hxS, fun e c â†¦ ?_âŸ©
  rcases eq_or_ne e 0 with rfl | he_ne; Â· simp [norm_nonneg]
  let e_norm := â€–eâ€–
  let e' : E := (e_normâ»Â¹ : ğ•œ) â€¢ e
  have he'_norm : â€–e'â€– = 1 := norm_smul_inv_norm he_ne
  have estimate : â€–e'  + (e_normâ»Â¹ * c) â€¢ xâ€– â‰¥ 1 - Îµ := by
    let c' := e_normâ»Â¹ * c
    rcases le_or_gt M â€–c'â€– with h_large | h_small
    Â· calc â€–e' + c' â€¢ xâ€–
        _ = â€–c' â€¢ x + e'â€–                       := by rw [add_comm]
        _ â‰¥ â€–c' â€¢ xâ€– - â€–(e' : StrongDual ğ•œ X)â€–  := norm_sub_le_norm_add _ _
        _ = â€–c'â€– * â€–xâ€– - 1                      := by rw [norm_smul, norm_coe, he'_norm]
        _ â‰¥ M * Î´ - 1                           := by gcongr; exact h_norm_S x hxS
        _ â‰¥ 1 - Îµ                               := by dsimp [M]; field_simp [hÎ´.ne']; nlinarith
    Â· obtain this := hF_cover (mem_sphere_zero_iff_norm.mpr he'_norm)
      rw [Set.mem_iUnionâ‚‚] at this
      obtain âŸ¨v, hvF, hv_lowerâŸ© := this
      calc â€–e' + c' â€¢ xâ€–
        _ â‰¥ â€–(e' + c' â€¢ x) vâ€–               := ContinuousLinearMap.unit_le_opNorm _ _ v.property
        _ = â€–(e' : StrongDual ğ•œ X) v + (c' â€¢ x) vâ€–          := by simp only
          [ContinuousLinearMap.add_apply, ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
        _ â‰¥ â€–(e' : StrongDual ğ•œ X) vâ€– - â€–c' â€¢ x vâ€–          := norm_sub_le_norm_add _ _
        _ â‰¥ â€–(e' : StrongDual ğ•œ X) vâ€– - â€–c'â€– * â€–x vâ€–        := by rw [norm_smul]
        _ â‰¥ (1 - Îµ / 2) - M * Î³          := by
            have : â€–x vâ€– < Î³ := hwW v (Finset.mem_coe.mp hvF)
            gcongr
            exact hv_lower.le
        _ = 1 - Îµ                        := by dsimp [M, Î³]; field_simp [hÎ´.ne']; ring
  have h_norm_ne : (e_norm : ğ•œ) â‰  0 := RCLike.ofReal_ne_zero.mpr (norm_ne_zero_iff.mpr he_ne)
  have hfactor : (e : StrongDual ğ•œ X) + c â€¢ x =
      (e_norm : ğ•œ) â€¢ ((e' : StrongDual ğ•œ X) + ((e_normâ»Â¹ : ğ•œ) * c) â€¢ x) := by
    simp only [e', smul_add, Submodule.coe_smul, smul_smul,
      mul_inv_cancelâ‚€ h_norm_ne, one_smul, â† mul_assoc, one_mul]
  rw [ge_iff_le, hfactor, norm_smul, RCLike.norm_ofReal, abs_norm, mul_comm (1 - Îµ)]
  gcongr; rw [â† RCLike.ofReal_inv]; exact estimate.le

theorem basic_sequence_selection_dual {S : Set (StrongDual ğ•œ X)}
    (h_weak_star : (0 : WeakDual ğ•œ X) âˆˆ closure (StrongDual.toWeakDual '' S))
    (h_norm : (0 : StrongDual ğ•œ X) âˆ‰ closure S)
    {Îµ : â„} (hÎµ : Îµ > 0) :
    âˆƒ (b : BasicSequence ğ•œ (StrongDual ğ•œ X)),
      (âˆ€ n, b n âˆˆ S) âˆ§
      b.basicSequenceConstant â‰¤ 1 + Îµ := by
  let u (n : â„•) := 1 + Îµ * (1 - (1/2) ^ n)
  let Î´ (n : â„•) := 1 - u n / u (n + 1)
  have hu : âˆ€ n, 1 â‰¤ u n âˆ§ u n < 1 + Îµ := fun n â†¦ by
    have hp : (1 / 2 : â„) ^ n â‰¤ 1 := pow_le_oneâ‚€ (by norm_num) (by norm_num)
    have hp' : 0 < (1 / 2 : â„) ^ n := pow_pos (by norm_num) n
    constructor <;> { dsimp [u]; nlinarith }
  have hÎ´_pos : âˆ€ n, 0 < Î´ n := fun n â†¦ by
    have hp : 0 < (1 / 2 : â„) ^ n := pow_pos (by norm_num) n
    dsimp [Î´, u]; rw [sub_pos, div_lt_one (by nlinarith [(hu (n + 1)).1])]
    nlinarith [show (1 / 2 : â„) ^ (n + 1) = 1 / 2 * (1 / 2) ^ n from by ring]
  have hu_pos : âˆ€ k, 0 < u k := fun k => lt_of_lt_of_le (by linarith) (hu k).1
  let f : â„• â†’ StrongDual ğ•œ X := fun n => Nat.strongRecOn n (fun k prev â†¦
    let E := Submodule.span ğ•œ (Set.range (fun i : Fin k â†¦ prev i i.isLt))
    Classical.choose (perturbation_finite_dimensional h_weak_star h_norm E
      (FiniteDimensional.span_of_finite ğ•œ (Set.finite_range _)) (hÎ´_pos k)))
  have hf_spec (n : â„•) :
      f n âˆˆ S âˆ§ âˆ€ (e : Submodule.span ğ•œ (Set.range (fun i : Fin n â†¦ f i))) (c : ğ•œ),
        (1 - Î´ n) * â€–eâ€– â‰¤ â€–(e : StrongDual ğ•œ X) + c â€¢ f nâ€– := by
    let P := perturbation_finite_dimensional h_weak_star h_norm
      (Submodule.span ğ•œ (Set.range (fun i : Fin n â†¦ f i)))
      (FiniteDimensional.span_of_finite ğ•œ (Set.finite_range _)) (hÎ´_pos n)
    have hfn : f n = Classical.choose P := by unfold f; rw [Nat.strongRecOn_eq]
    rw [hfn]; exact Classical.choose_spec P
  have h_grunblum_bound : âˆ€ n m (a : â„• â†’ ğ•œ), m â‰¤ n â†’
      â€–âˆ‘ i âˆˆ Finset.range m, a i â€¢ f iâ€– â‰¤ (1 + Îµ) * â€–âˆ‘ i âˆˆ Finset.range n, a i â€¢ f iâ€– := by
    intro n m a hnm
    let S (k : â„•) := âˆ‘ i âˆˆ Finset.range k, a i â€¢ f i
    have h_step (k) (hk : k < n) : â€–S kâ€– â‰¤ (1 - Î´ k)â»Â¹ * â€–S (k + 1)â€– := by
      have hSk_mem : S k âˆˆ Submodule.span ğ•œ (Set.range (fun i : Fin k â†¦ f i)) :=
        Submodule.sum_mem _ (fun i hi â†¦ Submodule.smul_mem _ _ <|
          Submodule.subset_span âŸ¨âŸ¨i, Finset.mem_range.mp hiâŸ©, rflâŸ©)
      let e : Submodule.span ğ•œ (Set.range (fun i : Fin k â†¦ f i)) := âŸ¨S k, hSk_memâŸ©
      have h := (hf_spec k).2 e (a k)
      simp only [e, S] at h
      have h1Î´ : 0 < 1 - Î´ k := by
        simp only [Î´, sub_sub_cancel]; exact div_pos (hu_pos k) (hu_pos (k+1))
      rw [le_inv_mul_iffâ‚€ h1Î´]
      calc (1 - Î´ k) * â€–S kâ€– â‰¤ â€–S k + a k â€¢ f kâ€– := h
        _ = â€–S (k + 1)â€– := by simp only [S, Finset.sum_range_succ]
    have h_inv : âˆ€ k, (1 - Î´ k)â»Â¹ = u (k + 1) / u k := fun k => by
      simp only [Î´, sub_sub_cancel]; rw [inv_div]
    have h_chain : â€–S mâ€– â‰¤ (u n / u m) * â€–S nâ€– := by
      obtain âŸ¨d, rflâŸ© := Nat.exists_eq_add_of_le hnm
      induction d with
      | zero => simp [(hu_pos m).ne']
      | succ d ih =>
        calc â€–S mâ€–
          _ â‰¤ (u (m + d) / u m) * â€–S (m + d)â€– :=
              ih (Nat.le_add_right m d) (fun k hk => h_step k (Nat.lt_add_right 1 hk))
          _ â‰¤ (u (m + d) / u m) * ((1 - Î´ (m + d))â»Â¹ * â€–S (m + d + 1)â€–) := by
              gcongr
              Â· exact div_nonneg (hu_pos _).le (hu_pos _).le
              Â· exact h_step (m + d) (by omega)
          _ = (u (m + (d + 1)) / u m) * â€–S (m + (d + 1))â€– := by
              rw [h_inv, show m + d + 1 = m + (d + 1) from by ring]
              field_simp [(hu_pos _).ne']
    calc â€–S mâ€– â‰¤ (u n / u m) * â€–S nâ€– := h_chain
      _ â‰¤ (1 + Îµ) * â€–S nâ€– := by
          gcongr; exact (div_le_self (hu_pos n).le (hu m).1).trans (hu n).2.le
  have h_nz n : f n â‰  0 := by
    intro hfn
    apply h_norm
    rw [â† hfn]
    exact subset_closure (hf_spec n).1
  obtain âŸ¨b, hb, hboundâŸ© := isBasicSequence_of_Grunblum_with_bound h_grunblum_bound h_nz
  refine âŸ¨b, ?_, hboundâŸ©
  intro n
  rw [show b n = f n from congrFun hb n]
  exact (hf_spec n).1

/-- In an infinite-dimensional normed space, `0` is in the weak* closure of the image of the
    unit sphere under the canonical embedding into the bidual. -/
lemma weak_closure_sphere_contains_zero (hinf : Â¬ FiniteDimensional ğ•œ X) :
    (0 : WeakDual ğ•œ (StrongDual ğ•œ X)) âˆˆ closure (
      StrongDual.toWeakDual '' (NormedSpace.inclusionInDoubleDual ğ•œ X '' Metric.sphere 0 1)) := by
  let J := NormedSpace.inclusionInDoubleDual ğ•œ X
  let S := StrongDual.toWeakDual '' (J '' Metric.sphere 0 1)
  rw [_root_.mem_closure_iff]
  intro U hU_open hU_zero
  rw [isOpen_induced_iff] at hU_open
  obtain âŸ¨V, hV_open, hV_eqâŸ© := hU_open
  have h0V : (fun f => (0 : WeakDual ğ•œ (StrongDual ğ•œ X)) f) âˆˆ V := by
    rw [â† hV_eq] at hU_zero; exact hU_zero
  rw [isOpen_pi_iff] at hV_open
  obtain âŸ¨F, t, ht_cond, hFt_subâŸ© := hV_open _ h0V
  let K := â¨… f âˆˆ F, LinearMap.ker (f : X â†’â‚—[ğ•œ] ğ•œ)
  have hK_nontrivial : K â‰  âŠ¥ := by
    by_contra h_bot
    have : FiniteDimensional ğ•œ X := by
      let Ï† := LinearMap.pi (fun (f : F) => (f : StrongDual ğ•œ X).toLinearMap)
      apply Module.Finite.of_injective Ï†
      intro x y hxy
      simp only [funext_iff] at hxy
      have hmem : x - y âˆˆ K := by
        simp only [K, Submodule.mem_iInf, LinearMap.mem_ker, map_sub, sub_eq_zero]
        exact fun f hf => hxy âŸ¨f, hfâŸ©
      rw [h_bot, Submodule.mem_bot] at hmem
      exact sub_eq_zero.mp hmem
    exact hinf this
  obtain âŸ¨v, hvK, hv_neâŸ© := Submodule.exists_mem_ne_zero_of_ne_bot hK_nontrivial
  let x := (â€–vâ€–â»Â¹ : ğ•œ) â€¢ v
  have hx_norm : â€–xâ€– = 1 := norm_smul_inv_norm hv_ne
  have hx_K : x âˆˆ K := K.smul_mem _ hvK
  have h_vanish : âˆ€ f âˆˆ F, (f : StrongDual ğ•œ X) x = 0 := fun f hf =>
    LinearMap.mem_ker.mp ((Submodule.mem_iInf _).mp ((Submodule.mem_iInf _).mp hx_K f) hf)
  have hJx_S : StrongDual.toWeakDual (J x) âˆˆ S :=
    âŸ¨J x, âŸ¨x, mem_sphere_zero_iff_norm.mpr hx_norm, rflâŸ©, rflâŸ©
  have hJx_U : StrongDual.toWeakDual (J x) âˆˆ U := by
    rw [â† hV_eq]
    apply hFt_sub
    intro f hf
    change topDualPairing ğ•œ (StrongDual ğ•œ X) (StrongDual.toWeakDual (J x)) f âˆˆ t f
    simp only [topDualPairing_apply, StrongDual.coe_toWeakDual, J, NormedSpace.dual_def]
    rw [h_vanish f hf]
    exact (ht_cond f hf).2
  exact âŸ¨StrongDual.toWeakDual (J x), hJx_U, hJx_SâŸ©

/-- Every infinite-dimensional Banach space contains a basic sequence with basis constant
    arbitrarily close to 1 (the Bessagaâ€“PeÅ‚czyÅ„ski theorem, [albiac2016, Corollary 1.5.3]). -/
theorem exists_basicSequence (hinf : Â¬ FiniteDimensional ğ•œ X) {Îµ : â„}
    (hÎµ : 0 < Îµ) : âˆƒ (b : BasicSequence ğ•œ X), b.basicSequenceConstant â‰¤ 1 + Îµ := by
  let J := NormedSpace.inclusionInDoubleDual ğ•œ X
  let S_bidual := J '' (Metric.sphere 0 1)
  have h_weak : (0 : WeakDual ğ•œ (StrongDual ğ•œ X)) âˆˆ
      closure (StrongDual.toWeakDual '' S_bidual) :=
    weak_closure_sphere_contains_zero hinf
  have h_norm : (0 : StrongDual ğ•œ (StrongDual ğ•œ X)) âˆ‰ closure S_bidual := by
    rw [Metric.mem_closure_iff]
    push_neg
    use 1, zero_lt_one
    rintro _ âŸ¨x, hx, rflâŸ©
    have hJ_iso : â€–J xâ€– = â€–xâ€– := (NormedSpace.inclusionInDoubleDualLi (ğ•œ := ğ•œ) (E := X)).norm_map x
    rw [dist_zero_left, hJ_iso, mem_sphere_zero_iff_norm.mp hx]
  obtain âŸ¨b_bidual, hb_mem, hb_constâŸ© := basic_sequence_selection_dual h_weak h_norm hÎµ
  obtain âŸ¨b, _, hb_boundâŸ© := b_bidual.pullback J
    (NormedSpace.inclusionInDoubleDualLi (ğ•œ := ğ•œ) (E := X)).norm_map hb_mem
  exact âŸ¨b, hb_bound.trans hb_constâŸ©

end BasicSequence
