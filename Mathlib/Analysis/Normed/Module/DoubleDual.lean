/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, MichaÅ‚ ÅšwiÄ™tek
-/
module

public import Mathlib.Analysis.Normed.Module.WeakSpace

/-!
# The double dual of a normed space

In this file we define the inclusion of a normed space into its double strong dual and prove
basic properties.

## Main definitions

* `NormedSpace.inclusionInDoubleDual` is the inclusion of a normed space in its double
  `StrongDual`, considered as a bounded linear map.
* `NormedSpace.inclusionInDoubleDualLi` is the same map as a linear isometry (for `ğ•œ = â„` or
  `ğ•œ = â„‚`).
* `NormedSpace.inclusionInDoubleDualWeak` is the canonical map from the weak space into the
  weak-star bidual.
* `NormedSpace.inclusionInDoubleDualWeak_isEmbedding` shows that `inclusionInDoubleDualWeak` is
  a topological embedding.
* `NormedSpace.inclusionInDoubleDualWeak_homeomorph` is the same map as a homeomorphism onto
  its range.
* `NormedSpace.isCompact_closure_of_isBounded` transfers compactness from the weak-star topology
  on the bidual back to the weak topology on `X` via Banachâ€“Alaoglu.

## References

* [Conway, John B., A course in functional analysis][conway1990]

## Tags

double dual, inclusion, isometry, embedding
-/

@[expose] public section

noncomputable section

open Topology Bornology WeakDual

universe u v

namespace NormedSpace

section General

variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
variable (E : Type*) [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]
variable (F : Type*) [NormedAddCommGroup F] [NormedSpace ğ•œ F]

/-- The inclusion of a normed space in its double (topological) strong dual, considered
as a bounded linear map. -/
def inclusionInDoubleDual : E â†’L[ğ•œ] StrongDual ğ•œ (StrongDual ğ•œ E) :=
  ContinuousLinearMap.apply ğ•œ ğ•œ

@[simp]
theorem dual_def (x : E) (f : StrongDual ğ•œ E) : inclusionInDoubleDual ğ•œ E x f = f x :=
  rfl

theorem inclusionInDoubleDual_norm_eq :
    â€–inclusionInDoubleDual ğ•œ Eâ€– = â€–ContinuousLinearMap.id ğ•œ (StrongDual ğ•œ E)â€– :=
  ContinuousLinearMap.opNorm_flip _

theorem inclusionInDoubleDual_norm_le : â€–inclusionInDoubleDual ğ•œ Eâ€– â‰¤ 1 := by
  rw [inclusionInDoubleDual_norm_eq]
  exact ContinuousLinearMap.norm_id_le

theorem double_dual_bound (x : E) : â€–(inclusionInDoubleDual ğ•œ E) xâ€– â‰¤ â€–xâ€– := by
  simpa using ContinuousLinearMap.le_of_opNorm_le _ (inclusionInDoubleDual_norm_le ğ•œ E) x

end General

section BidualIsometry

variable (ğ•œ : Type v) [RCLike ğ•œ] {E : Type u}

section Seminormed

variable [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- The inclusion of a normed space in its double strong dual is an isometry onto its image. -/
def inclusionInDoubleDualLi : E â†’â‚—áµ¢[ğ•œ] StrongDual ğ•œ (StrongDual ğ•œ E) :=
  { inclusionInDoubleDual ğ•œ E with
    norm_map' x := by
      apply le_antisymm (double_dual_bound ğ•œ E x)
      obtain âŸ¨g, hgâŸ© := exists_dual_vector'' ğ•œ x
      grw [â† (inclusionInDoubleDual ğ•œ E x).unit_le_opNorm g hg.left]
      simp [hg.right] }

/-- If one controls the norm of every `f x`, then one controls the norm of `x`.
Compare `ContinuousLinearMap.opNorm_le_bound`. -/
theorem norm_le_dual_bound (x : E) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ f : StrongDual ğ•œ E, â€–f xâ€– â‰¤ M * â€–fâ€–) : â€–xâ€– â‰¤ M := by
  rw [â† (inclusionInDoubleDualLi (E := E) ğ•œ).norm_map x]
  exact ContinuousLinearMap.opNorm_le_bound _ hMp hM

end Seminormed

end BidualIsometry

section Embedding

variable (ğ•œ : Type*) [RCLike ğ•œ] (X : Type*) [NormedAddCommGroup X] [NormedSpace ğ•œ X]

/-- The canonical map from a normed space (with the weak topology) into the weak-star bidual.
This is `inclusionInDoubleDual` composed with `StrongDual.toWeakDual`, bundling the topology
change on both sides. -/
def inclusionInDoubleDualWeak (x : WeakSpace ğ•œ X) : WeakDual ğ•œ (StrongDual ğ•œ X) :=
  StrongDual.toWeakDual (inclusionInDoubleDual ğ•œ X x)

/-- The canonical embedding into the weak-star bidual evaluates to `f x`. -/
@[simp]
theorem inclusionInDoubleDualWeak_apply (x : WeakSpace ğ•œ X) (f : StrongDual ğ•œ X) :
    (inclusionInDoubleDualWeak ğ•œ X x) f = f x :=
  rfl

/-- `inclusionInDoubleDualWeak` is inducing: the weak topology on `X` coincides with the topology
pulled back from the weak-star topology on the bidual. Both are the topology of pointwise
convergence against `StrongDual ğ•œ X`. -/
theorem inclusionInDoubleDualWeak_isInducing :
    IsInducing (inclusionInDoubleDualWeak ğ•œ X) where
  eq_induced := by
    change _ = TopologicalSpace.induced (inclusionInDoubleDualWeak ğ•œ X)
      (TopologicalSpace.induced
        (fun (Ï† : WeakDual ğ•œ (StrongDual ğ•œ X)) f => Ï† f) Pi.topologicalSpace)
    rw [induced_compose]
    rfl

/-- `inclusionInDoubleDualWeak` is a topological embedding from the weak topology to the weak-star
topology. -/
theorem inclusionInDoubleDualWeak_isEmbedding :
    IsEmbedding (inclusionInDoubleDualWeak ğ•œ X) where
  toIsInducing := inclusionInDoubleDualWeak_isInducing ğ•œ X
  injective := StrongDual.toWeakDual.injective.comp
    (inclusionInDoubleDualLi (ğ•œ := ğ•œ) (E := X)).injective

/-- The inclusion of a normed space into its double dual, as a homeomorphism onto its range,
where the domain carries the weak topology and the codomain the weak-star topology. -/
def inclusionInDoubleDualWeak_homeomorph :
    WeakSpace ğ•œ X â‰ƒâ‚œ Set.range (inclusionInDoubleDualWeak ğ•œ X) :=
  (inclusionInDoubleDualWeak_isEmbedding ğ•œ X).toHomeomorph

/-- If `S` is bounded and the weak-star closure of its image under the canonical embedding into the
double dual lies in the range of that embedding, then `closure S` is compact in the weak topology.

This combines Banachâ€“Alaoglu (compactness of bounded weak-starâ€“closed sets) with the topological
embedding `inclusionInDoubleDualWeak_isEmbedding` to transfer compactness back to the weak
topology on `X`. -/
theorem isCompact_closure_of_isBounded {S : Set (WeakSpace ğ•œ X)} (hb : IsBounded S)
    (hrange : closure (inclusionInDoubleDualWeak ğ•œ X '' S) âŠ†
      Set.range (inclusionInDoubleDualWeak ğ•œ X)) :
    IsCompact (closure S) := by
  rw [(inclusionInDoubleDualWeak_isInducing ğ•œ X).closure_eq_preimage_closure_image]
  apply (inclusionInDoubleDualWeak_isInducing ğ•œ X).isCompact_preimage' _ hrange
  obtain âŸ¨R, hRâŸ© := (Metric.isBounded_iff_subset_closedBall 0).mp
    ((inclusionInDoubleDual ğ•œ X).lipschitz.isBounded_image hb)
  refine (WeakDual.isCompact_closedBall (ğ•œ := ğ•œ) (E := StrongDual ğ•œ X) 0 R).of_isClosed_subset
    isClosed_closure (closure_minimal ?_ (WeakDual.isClosed_closedBall 0 R))
  rintro _ âŸ¨z, hz, rflâŸ©
  exact hR âŸ¨z, hz, rflâŸ©

end Embedding

end NormedSpace
