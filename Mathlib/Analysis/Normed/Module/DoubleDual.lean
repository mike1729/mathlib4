/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
module

public import Mathlib.Analysis.Normed.Module.WeakDual
public import Mathlib.Analysis.Normed.Module.HahnBanach

/-!
# The double dual of a normed space

In this file we define the inclusion of a normed space into its double strong dual and prove
basic properties.

## Main definitions

* `NormedSpace.inclusionInDoubleDual` is the inclusion of a normed space in its double
  `StrongDual`, considered as a bounded linear map.
* `NormedSpace.inclusionInDoubleDualLi` is the same map as a linear isometry (for `ğ•œ = â„` or
  `ğ•œ = â„‚`).
* `NormedSpace.inclusionInDoubleDual_isEmbedding_weak` shows that the inclusion is an embedding
  from the weak topology to the weak-star topology.
* `NormedSpace.inclusionInDoubleDual_homeomorph_weak` is the same map as a homeomorphism onto
  its range.

## References

* [Conway, John B., A course in functional analysis][conway1990]

## Tags

double dual, inclusion, isometry, embedding
-/

@[expose] public section

noncomputable section

open Topology Bornology WeakDual

universe u v

namespace NormedSpace

section General

variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
variable (E : Type*) [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]
variable (F : Type*) [NormedAddCommGroup F] [NormedSpace ğ•œ F]

/-- The inclusion of a normed space in its double (topological) strong dual, considered
as a bounded linear map. -/
def inclusionInDoubleDual : E â†’L[ğ•œ] StrongDual ğ•œ (StrongDual ğ•œ E) :=
  ContinuousLinearMap.apply ğ•œ ğ•œ

@[simp]
theorem dual_def (x : E) (f : StrongDual ğ•œ E) : inclusionInDoubleDual ğ•œ E x f = f x :=
  rfl

theorem inclusionInDoubleDual_norm_eq :
    â€–inclusionInDoubleDual ğ•œ Eâ€– = â€–ContinuousLinearMap.id ğ•œ (StrongDual ğ•œ E)â€– :=
  ContinuousLinearMap.opNorm_flip _

theorem inclusionInDoubleDual_norm_le : â€–inclusionInDoubleDual ğ•œ Eâ€– â‰¤ 1 := by
  rw [inclusionInDoubleDual_norm_eq]
  exact ContinuousLinearMap.norm_id_le

theorem double_dual_bound (x : E) : â€–(inclusionInDoubleDual ğ•œ E) xâ€– â‰¤ â€–xâ€– := by
  simpa using ContinuousLinearMap.le_of_opNorm_le _ (inclusionInDoubleDual_norm_le ğ•œ E) x

end General

section BidualIsometry

variable (ğ•œ : Type v) [RCLike ğ•œ] {E : Type u}

section Seminormed

variable [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- The inclusion of a normed space in its double strong dual is an isometry onto its image. -/
def inclusionInDoubleDualLi : E â†’â‚—áµ¢[ğ•œ] StrongDual ğ•œ (StrongDual ğ•œ E) :=
  { inclusionInDoubleDual ğ•œ E with
    norm_map' x := by
      apply le_antisymm (double_dual_bound ğ•œ E x)
      obtain âŸ¨g, hgâŸ© := exists_dual_vector'' ğ•œ x
      grw [â† (inclusionInDoubleDual ğ•œ E x).unit_le_opNorm g hg.left]
      simp [hg.right] }

/-- If one controls the norm of every `f x`, then one controls the norm of `x`.
Compare `ContinuousLinearMap.opNorm_le_bound`. -/
theorem norm_le_dual_bound (x : E) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ f : StrongDual ğ•œ E, â€–f xâ€– â‰¤ M * â€–fâ€–) : â€–xâ€– â‰¤ M := by
  rw [â† (inclusionInDoubleDualLi (E := E) ğ•œ).norm_map x]
  exact ContinuousLinearMap.opNorm_le_bound _ hMp hM

end Seminormed

variable [NormedAddCommGroup E] [NormedSpace ğ•œ E]

theorem eq_zero_of_forall_dual_eq_zero {x : E} (h : âˆ€ f : StrongDual ğ•œ E, f x = (0 : ğ•œ)) : x = 0 :=
  norm_le_zero_iff.mp (norm_le_dual_bound ğ•œ x le_rfl fun f => by simp [h f])

theorem eq_zero_iff_forall_dual_eq_zero (x : E) : x = 0 â†” âˆ€ g : StrongDual ğ•œ E, g x = 0 :=
  âŸ¨fun hx => by simp [hx], fun h => eq_zero_of_forall_dual_eq_zero ğ•œ hâŸ©

/-- See also `geometric_hahn_banach_point_point`. -/
theorem eq_iff_forall_dual_eq {x y : E} : x = y â†” âˆ€ g : StrongDual ğ•œ E, g x = g y := by
  rw [â† sub_eq_zero, eq_zero_iff_forall_dual_eq_zero ğ•œ (x - y)]
  simp [sub_eq_zero]

end BidualIsometry

section Embedding

variable (ğ•œ : Type*) [RCLike ğ•œ] (X : Type*) [NormedAddCommGroup X] [NormedSpace ğ•œ X]

/-- The map `WeakSpace ğ•œ X â†’ WeakDual ğ•œ (StrongDual ğ•œ X)` induced by `inclusionInDoubleDual`
is a topological embedding. That is, the canonical inclusion of a normed space into its double dual
is an embedding when the domain carries the weak topology and the codomain the weak-star topology.

The proof shows that both topologies on the domain are the topology of pointwise convergence
against `StrongDual ğ•œ X`. -/
theorem inclusionInDoubleDual_isEmbedding_weak :
    IsEmbedding (fun x : WeakSpace ğ•œ X => StrongDual.toWeakDual (inclusionInDoubleDual ğ•œ X x)) := by
  let Î¹ := fun x : WeakSpace ğ•œ X => StrongDual.toWeakDual (inclusionInDoubleDual ğ•œ X x)
  let evalWeakSpace : WeakSpace ğ•œ X â†’ (StrongDual ğ•œ X â†’ ğ•œ) := fun x f => f x
  let evalWeakDual : WeakDual ğ•œ (StrongDual ğ•œ X) â†’ (StrongDual ğ•œ X â†’ ğ•œ) := fun Ï† f => Ï† f
  have h_commute : evalWeakDual âˆ˜ Î¹ = evalWeakSpace := by ext x f; rfl
  have h_inj : Function.Injective Î¹ :=
    StrongDual.toWeakDual.injective.comp (inclusionInDoubleDualLi (ğ•œ := ğ•œ) (E := X)).injective
  have h_ind : IsInducing Î¹ := by
    constructor; symm
    calc TopologicalSpace.induced Î¹ (TopologicalSpace.induced evalWeakDual Pi.topologicalSpace)
        = TopologicalSpace.induced (evalWeakDual âˆ˜ Î¹) Pi.topologicalSpace := induced_compose
      _ = TopologicalSpace.induced evalWeakSpace Pi.topologicalSpace := by rw [h_commute]
  exact âŸ¨h_ind, h_injâŸ©

/-- The inclusion of a normed space into its double dual, as a homeomorphism onto its range,
where the domain carries the weak topology and the codomain the weak-star topology. -/
def inclusionInDoubleDual_homeomorph_weak :
    WeakSpace ğ•œ X â‰ƒâ‚œ Set.range (fun x : WeakSpace ğ•œ X =>
      StrongDual.toWeakDual (inclusionInDoubleDual ğ•œ X x)) :=
  (inclusionInDoubleDual_isEmbedding_weak ğ•œ X).toHomeomorph

end Embedding

end NormedSpace
